WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }
spacename = @{ ASCII_ALPHANUMERIC+ ~ ( "." ~ ASCII_ALPHANUMERIC*)* }
entityname = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHANUMERIC*}
attributename = @{ ASCII_ALPHANUMERIC+ }
parametername = @{ ASCII_ALPHANUMERIC+ }
enummembername = @{ ASCII_ALPHANUMERIC+ }
enummembervalue = @{ ASCII_ALPHANUMERIC+ }
parameter = { parametername ~ ":" ~ entityname}
operationname = @{ ASCII_ALPHANUMERIC+ }
singlemult = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
multimult = { singlemult ~ "," ~ singlemult }
listmult = { "*" | "+" }
optionalmult = {"?"}
multiplicity = { ("[" ~ (multimult | singlemult | listmult) ~ "]") | optionalmult }
parententityname = @{ entityname }
attribute = { attributename ~ ":" ~ entityname ~ multiplicity? }
parameterlist = { (parameter ~ ("," ~ parameter)*)? }
operation = { operationname ~ "(" ~ parameterlist ~ ")" ~ (":" ~ entityname)? }
feature = { attribute | operation }
entity = { entityname ~ (":" ~ parententityname)? ~"{" ~ (feature ~ ("," ~ feature)*)? ~ "}"}
enummember = { enummembername ~ ("=" ~ enummembervalue)? }
enumeration = { entityname ~ "{" ~ (enummember ~ ("," ~enummember)*)? ~ "}" }
entitytype = { enumeration | entity }
namespace = { spacename ~ "{" ~ entitytype* ~ "}" }
pakken = _{ SOI ~ namespace ~ EOI}